plugins {
    id 'org.unbroken-dome.test-sets' version '2.2.1' apply false
    id 'org.ajoberstar.grgit' version '4.0.2'
    id "org.sonarqube" version "2.8"
}

def baseVersion = '0.1.0'
def commitHash = grgit.head().abbreviatedId
def buildNum = System.env.CIRCLE_BUILD_NUM ?: '0'
def fullVersion = "${baseVersion}.${buildNum}-${commitHash}"

subprojects {
    apply plugin: 'java'
    apply plugin: 'jacoco'
    apply plugin: 'org.unbroken-dome.test-sets'

    version = fullVersion

    sourceCompatibility = 11
    targetCompatibility = 11

    repositories {
        jcenter()
    }

    jacocoTestReport {
        reports {
            xml.enabled true
        }
    }
}

task testReport(type: TestReport) {
    destinationDir = file("$buildDir/reports/allTests")
    // Include the results from the `test` task in all subprojects
    reportOn subprojects*.test
}

task test {
    finalizedBy testReport
}

task check {
    finalizedBy testReport
}

task dumpVersion {
    def versionFile = file("$buildDir/version.txt")
    doLast {
        versionFile.text = fullVersion
    }
    inputs.property 'fullVersion', fullVersion
    outputs.file versionFile
}
task assemble {
    dependsOn dumpVersion
}

sonarqube {
  properties {
    property "sonar.projectKey", "adnovum_gong-notifier"
    property "sonar.organization", "adnovum"
    property "sonar.host.url", "https://sonarcloud.io"
  }
}

subprojects*.tasks*.jacocoTestReport.each { tasks.sonarqube.dependsOn it }
